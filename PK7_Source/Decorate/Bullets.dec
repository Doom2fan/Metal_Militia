/* Credits:
** Decorate: WildWeasel, Chronos "phantombeta" Ouroboros
** Sprites: ???
** Weapons: A_FireCustomMissile (tracerName, fRandom [weaponSpread] (-, horizontalSpread), TRUE, horizontalOffset, verticalOffset, 0, fRandom [weaponSpread] (-, verticalSpread))
** Monsters: A_CustomMissile (tracerName, verticalOffset, horizontalOffset, fRandom [monsterSpread] (-, horizontalSpread), 0, fRandom [monsterSpread] (-, verticalSpread))
*/

/*** Base ***/
actor MM_TracerEffectsBase {
    height 2
    radius 2
    alpha 1.0
    scale 0.025
    renderStyle add
    +noBlockmap +noGravity +noTeleport +cannotPush
    +noInteraction +clientsideOnly +forceXYBillboard
}

actor MM_BaseTracer : fastProjectile {
    alpha 1.0
    renderStyle add
    radius 2
    height 2
    speed 25
    health 75
    scale 0.02120
    damage (random (1, 3) * 4)
    missileType "MM_TracerTrail"
    missileHeight 8
    decal BulletChip

    +bloodSplatter +noExtremeDeath +noDamageThrust +puffGetsOwner
    +forceXYBillboard

    states {
    Spawn:
        TNT1 AA 0 A_ScaleVelocity (1.0 / (25 * 1.0))
        TNT1 A  0 A_ScaleVelocity (health * 1.0)
        TNT1 A  1 A_Jump (256, "Idle")
        wait
    Idle:
        SPRK A 1 bright
        wait

    Death:
    Crash:
        TNT1 AAAAAA 0        A_SpawnItemEx ("MM_BulletPuffSparks", 0.0, 0.0, 0.0, fRandom [sfx] (0.0, 2.0), fRandom [sfx] (-2.0, 2.0), fRandom [sfx] (1.5, 4.0), 0, SXF_Clientside)
        TNT1 AAA    0        A_SpawnItemEx ("MM_BulletPuffSmoke", 0.0, 0.0, 0.0, 0.0, fRandom [sfx] (-1.0, 1.0), fRandom [sfx] (-0.8, 0.8), 0, SXF_Clientside)
        SPRK A      2 bright
        SPRK A      2 bright A_SetScale (scaleX * 1.5, scaleY * 1.5)
        stop
    XDeath:
        TNT1 A 3 A_PlaySound ("Tracers/HitFlesh", CHAN_Body)
        stop
    }
}

actor MM_TracerTrail : MM_TracerEffectsBase {
    states {
    Spawn:
        SPRK AAA 1 bright A_FadeOut (0.4)
        stop
    }
}

actor MM_BulletPuff : MM_TracerEffectsBase replaces BulletPuff {
    decal BulletChip
    scale 0.1

    +puffGetsOwner +noExtremeDeath
    -clientsideOnly -noInteraction
    states {
    Spawn:
        TNT1 AAAAAA 0 noDelay A_SpawnItemEx ("MM_BulletPuffSparks", 0.0, 0.0, 0.0, fRandom [sfx] (0.0, 2.0), fRandom [sfx] (-2.0, 2.0), fRandom [sfx] (1.5, 4.0), 0, SXF_Clientside)
        TNT1 AAA    0 A_SpawnItemEx ("MM_BulletPuffSmoke", 0.0, 0.0, 0.0, 0.0, fRandom [sfx] (-1.0, 1.0), fRandom [sfx] (-0.8, 0.8), 0, SXF_Clientside)
        SPRK A      2 bright
        stop
    }
}

actor MM_BulletPuffSmoke : MM_BlackSmoke {
    alpha 0.2
    states {
    Spawn:
        SMOK A 8
        SMOK A 1 A_FadeOut (0.03)
        wait
    }
}

actor MM_BulletPuffSparks : MM_TracerEffectsBase {
    mass 1
    +dropOff +ghost +dontSplash
    -doomBounce -noGravity -noInteraction
    speed 10
    gravity 1.0
    states {
    Spawn:
        SPRK A 1 bright
        SPRK A 1 bright A_FadeOut (0.05)
        wait
    }
}

// Tracers
actor MM_C1_AutoSGTracer : MM_BaseTracer {
    damage (random [weaponDamage] (1, 3) * 6)
    health 125
    damageType ShotgunTracer
}
actor MM_C1_AutoSGTracer2 : MM_C1_AutoSGTracer { damage (random [weaponDamage] (1, 3) * 5) }