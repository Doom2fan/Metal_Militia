/* Credits:
** Decorate: WildWeasel, Chronos "phantombeta" Ouroboros
** Sprites: ???
** Weapons: A_FireCustomMissile (tracerName, fRandom [weaponSpread] (-, horizontalSpread), 1, horizontalOffset, verticalOffset, 0, fRandom [weaponSpread] (-, verticalSpread))
** Monsters: A_CustomMissile (tracerName, verticalOffset, horizontalOffset, fRandom [monsterSpread] (-, horizontalSpread), 0, fRandom [monsterSpread] (-, verticalSpread))
*/

/*** Base ***/
actor DME_TracerEffectsBase {
    height 2
    radius 2
    alpha 1.0
    scale 0.025
    renderStyle add
    +noBlockmap +noGravity +noTeleport +cannotPush
    +noInteraction +clientsideOnly +forceXYBillboard
}

actor DME_BaseTracer : fastProjectile {
    alpha 1.0
    renderStyle add
    radius 2
    height 2
    speed 25
    args 100, 1, 8, 1, 3
    // args [speed, numBullets, damage, damageRandomMin, damageRandomMax]
    damage (random (1, 3) * 6)
    decal BulletChip

    projectile
    +bloodSplatter +noExtremeDeath +noDamageThrust +puffGetsOwner
    states {
    Spawn:
        TNT1 AA 0 A_ScaleVelocity (1.0 / (25 * 1.0))
        TNT1 A  0 A_ScaleVelocity (args [0] * 1.0)
        TNT1 A  0 A_Jump (256, "Spawn2")
        wait
    Spawn2:
        TNT1 A 0 A_SetPitch (CallACS ("DME_GetProjectilePitch") / 65535.0)
        TNT1 A 0 A_CustomBulletAttack (0.0, 0.0, args [1], args [2] * random (args [3], args [4]), "DME_BulletPuff", 32767.0, CBAF_AimFacing | CBAF_NoRandom | CBAF_ExplicitAngle)
        TNT1 A 0 A_SetPitch (0)
        TNT1 A 0 A_Jump (256, "Idle")
        wait
    Idle:
        SPRK A 1 bright
        wait
    Death:
        TNT1 A 1
        stop
    }
}

actor DME_BulletPuff : DME_TracerEffectsBase replaces BulletPuff {
    decal BulletChip
    scale 0.1

    +puffGetsOwner +noExtremeDeath
    -clientsideOnly -noInteraction
    states {
    Spawn:
        TNT1 AAAAAA 0 noDelay A_SpawnItemEx ("DME_BulletPuffSparks", 0.0, 0.0, 0.0, fRandom [sfx] (0.0, 2.0), fRandom [sfx] (-2.0, 2.0), fRandom [sfx] (1.5, 4.0), 0, SXF_Clientside)
        TNT1 AAA    0 A_SpawnItemEx ("DME_BulletPuffSmoke", 0.0, 0.0, 0.0, 0.0, fRandom [sfx] (-1.0, 1.0), fRandom [sfx] (-0.8, 0.8), 0, SXF_Clientside)
        SPRK A      2 bright
        stop
    }
}

actor DME_BulletPuffSmoke : DME_BlackSmoke {
    alpha 0.2
    states {
    Spawn:
        SMOK A 8
        SMOK A 1 A_FadeOut (0.03)
        wait
    }
}

actor DME_BulletPuffSparks : DME_TracerEffectsBase {
    mass 1
    +dropOff +ghost +dontSplash
    -doomBounce -noGravity -noInteraction
    speed 10
    gravity 1.0
    states {
    Spawn:
        SPRK A 1 bright
        SPRK A 1 bright A_FadeOut (0.05)
        wait
    }
}