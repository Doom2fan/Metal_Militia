// By Chronos "phantombeta" Ouroboros
actor DME_IsUnderwater : boolean { }
actor DME_Dying : boolean { }

actor DME_Player : playerPawn {
    // Damage
    gibHealth -50
    painChance 255
    
    // Movement
    player.ForwardMove 1.2, 1.2
    player.SideMove 1.2, 1.2
    player.JumpZ 9.797
    
    // Give Items/Weapons/Ammo
    player.StartItem DME_Pistol
    player.StartItem DME_9mmCartridges, 65
    player.StartItem DME_MeleeKey
    
    // Class stuff
    player.DisplayName "Marine"
    
    // Weapon Slots
    player.WeaponSlot 2, DME_Pistol
    player.WeaponSlot 3, DME_Shotgun, DME_SuperShotgun
    
    // Misc
    radius 16
    height 56
    player.ViewHeight 48
    player.AttackZOffset 20
    player.InvulnerabilityMode Reflective
    player.ColorRange 112, 127
    +solid +noSkin // +ghost
    
    var int user_Step;
    
    states {
    Spawn:
        PLAY AAAA 1
        loop
    See:
        TNT1 A      0 A_JumpIf (user_Step >= 1, "See2")
        TNT1 A      0 A_SetUserVar ("user_Step", TRUE)
        PLAY AAAAAA 1
        PLAY BBBBBB 1
        goto Spawn
    See2:
        TNT1 A      0 A_SetUserVar ("user_Step", FALSE)
        PLAY CCCCCC 1
        PLAY DDDDDD 1
        goto Spawn
    Missile:
        PLAY E 12
        goto Spawn
    Melee:
        PLAY F 6 bright
        goto Missile
    Pain:
        PLAY G 4
        PLAY G 4 A_Pain
        goto Spawn
    Death.Suicide:
    Death:
        PLAY H  10 A_PlayerScream
        PLAY I  10
        PLAY J  10 A_NoBlocking
        PLAY K  10
        TNT1 A   0 A_PlaySound ("Misc/Body", 5)
        PLAY LM 10
        PLAY N -1
        stop
    XDeath:
        PLAY O     5
        PLAY P     5 A_XScream
        PLAY Q     5 A_NoBlocking
        PLAY RSTUV 5
        PLAY W -1
        stop
    Death.Fire:
        BURN ABC        5 bright
        BURN D          5 bright
        BURN EFGHIJKLMN 5 bright
        BURN O          5 bright A_NoBlocking
        BURN PQRSTU     5 bright
        BURN V -1
        stop
    }
}

// Null Weapon
actor DME_NullWeapon : weapon {
    tag "Unarmed"
    +inventory.Undroppable +weapon.Wimpy_Weapon +weapon.CheatNotWeapon +noInteraction
    +weapon.NoAlert
    height 0
    radius 0
    states {
    Spawn:
        TNT1 A 0
        stop
    Select:
        TNT1 A 1 A_Raise
        TNT1 A 0 A_Raise
        wait
    Deselect:
        TNT1 A 0 A_Lower
        wait
    Ready:
        TNT1 A 1 A_WeaponReady (WRF_NoFire | WRF_NoBob | WRF_NoSwitch)
        loop
    Fire:
        TNT1 A 1
        goto Ready
    }
}

// Kicking
actor DME_DoMelee : boolean { }
actor DME_MeleeKey : customInventory {
    inventory.Amount 1
    inventory.MaxAmount 1
    +inventory.Undroppable -inventory.InvBar
    states {
    Use:
        TNT1 A 0 A_GiveInventory ("DME_DoMelee", 1)
        fail
    }
}