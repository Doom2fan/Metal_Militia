actor DME_ShotgunClip : ammo {
    inventory.MaxAmount 9
    ammo.BackpackMaxAmount 9
    +ignoreSkill
}

actor DME_Shotgun_Reloading : boolean { }
actor DME_Shotgun : DME_BaseWeapon {
    weapon.AmmoUse 1
    weapon.AmmoType "DME_ShotgunClip"
    weapon.AmmoType2 "DME_Shells"
    weapon.AmmoGive2 8
    obituary "$OB_MPSHOTGUN"
    tag "$TAG_SHOTGUN"
    states {
    Ready:
        SHTS CBA 1
    Ready2:
        TNT1 A 0 A_JumpIfInventory ("DME_DoMelee", 1, "KickMelee")
        SHTG A 1 A_WeaponReady (WRF_AllowReload)
        TNT1 A 0 A_TakeInventory ("DME_Shotgun_Reloading", 1)
        Loop

    Deselect:
        TNT1 A   0 A_TakeInventory ("DME_Shotgun_Reloading", 1)
        SHTS ABC 1
        goto BaseDeselect

    Fire:
        TNT1 A          0 A_JumpIfNoAmmo ("DryFire")
        TNT1 A          0 A_AlertMonsters
        TNT1 A          0 A_PlaySound ("Weapons/Shotgun/Fire", CHAN_Weapon)
        TNT1 A          0 A_GunFlash
        TNT1 AAAAAAAA   0 A_FireBullets (2.5, 3.2, -1, random (1, 3) * 12, "DME_BulletPuff", FBF_NoFlash | FBF_NoRandom)
        SHTG A          3 A_FireBullets (2.5, 3.2, -1, random (1, 3) * 12, "DME_BulletPuff", FBF_UseAmmo | FBF_NoFlash | FBF_NoRandom)
        SHTG ABCDDDCBAA 3
        SHTG A          1 A_Refire
        Goto Ready2
    Flash:
        SHTF A 1
        stop

    Reload:
        TNT1 A 0 A_JumpIfInventory ("DME_Shotgun_Reloading", 1, "ReloadFinish")
        TNT1 A 0 A_JumpIfInventory ("DME_ShotgunClip", 0, "Ready2")
        TNT1 A 0 A_JumpIfInventory ("DME_Shells", 1, "TrueReloadStart")
        goto Ready2

    TrueReloadStart:
        TNT1 A     0 A_GiveInventory ("DME_Shotgun_Reloading", 1)
        SHTR ABCDE 1
    TrueReload.FirstShell:
        TNT1 A                  0 A_JumpIfInventory ("DME_ShotgunClip", 1, "TrueReload")
        TNT1 A                  0 A_JumpIfInventory ("DME_Shells", 1, 1)
        goto ReloadFinish
        SHTR FGHI               1
        SHTR J                  1 A_PlaySound ("Weapons/Shotgun/In", CHAN_Weapon)
        TNT1 A                  0 A_GiveInventory ("DME_ShotgunClip", 1)
        TNT1 A                  0 A_TakeInventory ("DME_Shells", 1)
        SHTR KLMNO              1
        SHTR FFFPQRSTUVVUTSRQPF 1
        SHTR F                  2 A_WeaponReady (WRF_AllowReload | WRF_NoFire | WRF_NoSwitch | WRF_NoBob)
    TrueReload:
        TNT1 A     0 A_JumpIfInventory ("DME_ShotgunClip", 0, "ReloadFinish")
        TNT1 A     0 A_JumpIfInventory ("DME_Shells", 1, 1)
        goto ReloadFinish
        SHTR FGHI  1
        SHTR J     1 A_PlaySound ("Weapons/Shotgun/In", CHAN_Weapon)
        TNT1 A     0 A_GiveInventory ("DME_ShotgunClip", 1)
        TNT1 A     0 A_TakeInventory ("DME_Shells", 1)
        SHTR KLMNO 1
        SHTR F     3 A_WeaponReady (WRF_AllowReload | WRF_NoFire | WRF_NoSwitch | WRF_NoBob)
        loop
    ReloadFinish:
        TNT1 A     0 A_TakeInventory ("DME_Shotgun_Reloading", 1)
        SHTR EDCBA 1
        goto Ready2

    Spawn:
        SHTG Z -1
        stop
    }
}