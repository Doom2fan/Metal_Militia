actor MM_C1_AutoShotgun_TacticalReload : boolean { }
actor MM_C1_AutoShotgun_Clip : ammo {
    inventory.MaxAmount 12
    ammo.BackpackMaxAmount 12
    +ignoreSkill
}

actor MM_C1_AutoShotgun : MM_BaseWeapon {
    weapon.AmmoUse 1
    weapon.AmmoType "MM_C1_AutoShotgun_Clip"
    weapon.AmmoType2 "MM_Shells"
    weapon.AmmoGive2 12
    obituary "$OB_C1AUTOSG"
    tag "$TAG_C1AUTOSG"
    states {
    Ready:
        1SG0 ZYXWA 1
    Ready2:
        TNT1 A 0 A_JumpIfInventory ("MM_DoMelee", 1, "KickMelee")
        1SG0 A 1 A_WeaponReady (WRF_AllowReload)
        Loop

    Deselect:
        TNT1 AWXYZ 1
        goto BaseDeselect

    Fire:
        TNT1 A          0 A_JumpIfNoAmmo ("DryFire")
        TNT1 A          0 A_AlertMonsters
        TNT1 A          0 A_PlaySound ("Weapons/Class1/AutoSG/Fire", CHAN_Weapon)
        TNT1 A          0 A_GunFlash
        TNT1 AAAAAAAAAA 0 A_FireCustomMissile ("MM_C1_AutoSGTracer",  fRandom [weaponSpread] (-2.9, 2.9), FALSE, 0, 0, 0, fRandom [weaponSpread] (-2.2, 2.2))
        TNT1 AAAA       0 A_FireCustomMissile ("MM_C1_AutoSGTracer2", fRandom [weaponSpread] (-1.5, 1.5), FALSE, 0, 0, 0, fRandom [weaponSpread] (-0.8, 0.8))
        1SG0 A          1 A_FireCustomMissile ("MM_C1_AutoSGTracer2", fRandom [weaponSpread] (-1.5, 1.5), TRUE,  0, 0, 0, fRandom [weaponSpread] (-0.8, 0.8))

        1SG0 BCCBB 1
        1SG0 A     4
        1SG0 AAAA  1 A_WeaponReady (WRF_NoBob | WRF_DisableSwitch | WRF_NoSecondary)
        1SG0 A     3 A_Refire
        Goto Ready2
    Flash:
        1SGF A 1
        stop

    Reload:
        TNT1 A 0 A_JumpIfInventory ("MM_C1_AutoShotgun_Clip", 12, "Ready2")
        TNT1 A 0 A_JumpIfInventory ("MM_Shells", 1, "TrueReloadStart")
        goto Ready2
    TrueReloadStart:
        TNT1 A 0 A_JumpIfInventory ("MM_C1_AutoShotgun_Clip", 1, 2)
        TNT1 A 0 A_Jump (256, "TrueReload")
        TNT1 A 0 A_GiveInventory ("MM_C1_AutoShotgun_TacticalReload", 1)
    TrueReload:
        TNT1 A 0 A_JumpIfInventory ("MM_C1_AutoShotgun_Clip", 12, "ReloadFinish")
        TNT1 A 0 A_JumpIfInventory ("MM_Shells", 1, 1)
        goto ReloadFinish
        TNT1 A 0 A_GiveInventory ("MM_C1_AutoShotgun_Clip", 1)
        TNT1 A 0 A_TakeInventory ("MM_Shells", 1)
        loop
    ReloadFinish:
        1SG0 A       2
        1SG0 DEFGGG  1
        TNT1 A       0 A_PlaySound ("Weapons/Class1/AutoSG/Out", CHAN_Weapon)
        1SG0 HIJKGGG 2
        1SG0 G       5
        TNT1 A       0 A_PlaySound ("Weapons/Class1/AutoSG/In", CHAN_Weapon)
        1SG0 GLMNGGG 2
        1SG0 GGGGFED 1
        1SG0 A       3 A_JumpIfInventory ("MM_C1_AutoShotgun_TacticalReload", 1, "ReloadAnim.End")
        TNT1 A       0 A_PlaySound ("Weapons/Class1/AutoSG/Pump")
        1SG0 OPOA    3
    ReloadAnim.End:
        1SG0 A 2 A_TakeInventory ("MM_C1_AutoShotgun_TacticalReload", 9999)
        goto Ready2
    }
}