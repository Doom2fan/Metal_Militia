actor MM_PistolTacticalReload : boolean { }
actor MM_PistolClip : ammo {
    inventory.MaxAmount 16
    ammo.BackpackMaxAmount 16
    +ignoreSkill
}

actor MM_Pistol : MM_BaseWeapon {
    weapon.AmmoUse 1
    weapon.AmmoType "MM_PistolClip"
    weapon.AmmoType2 "MM_9mmCartridges"
    weapon.AmmoGive2 15
    obituary "$OB_MPPISTOL"
    tag "$TAG_PISTOL"
    states {
    Ready:
        PSTS CBA 1
    Ready2:
        TNT1 A 0 A_JumpIfInventory ("MM_DoMelee", 1, "KickMelee")
        PIST A 1 A_WeaponReady (WRF_AllowReload)
        Loop

    Deselect:
        PSTS ABC 1
        goto BaseDeselect

    Fire:
        TNT1 A    0 A_JumpIfNoAmmo ("DryFire")
        TNT1 A    0 A_AlertMonsters
        TNT1 A    0 A_PlaySound ("Weapons/Pistol/Fire", CHAN_Weapon)
        TNT1 A    0 A_GunFlash
        PIST B    1 A_FireBullets (0.8, 1.3, -1, random (1, 3) * 12, "MM_BulletPuff", FBF_UseAmmo | FBF_NoFlash | FBF_NoRandom)
        PIST CEED 1
        PIST CA   1 A_WeaponReady (WRF_NoBob | WRF_DisableSwitch | WRF_NoSecondary)
        Goto Ready2
    Flash:
        PSTF A 1
        stop

    Reload:
        TNT1 A 0 A_JumpIfInventory ("MM_PistolClip", 15, "Ready2")
        TNT1 A 0 A_JumpIfInventory ("MM_9mmCartridges", 1, "TrueReloadStart")
        goto Ready2
    TrueReloadStart:
        TNT1 A 0 A_JumpIfInventory ("MM_PistolClip", 1, 2)
        TNT1 A 0 A_Jump (256, 2)
        TNT1 A 0 A_GiveInventory ("MM_PistolTacticalReload", 1)
        TNT1 A 0 A_TakeInventory ("MM_PistolClip", 16)
    TrueReload:
        TNT1 A 0 A_JumpIfInventory ("MM_PistolClip", 15, "ReloadFinish")
        TNT1 A 0 A_JumpIfInventory ("MM_9mmCartridges", 1, 1)
        goto ReloadFinish
        TNT1 A 0 A_GiveInventory ("MM_PistolClip", 1)
        TNT1 A 0 A_TakeInventory ("MM_9mmCartridges", 1)
        loop
    ReloadFinish:
        TNT1 A 0 A_JumpIfInventory ("MM_PistolTacticalReload", 1, 2)
        TNT1 A 0 A_Jump (256, "ReloadAnim")
        TNT1 A 0 A_GiveInventory ("MM_PistolClip", 1)
    ReloadAnim:
        TNT1 A             0 A_TakeInventory ("MM_PistolTacticalReload", 9999)
        PIST A             2
        PSTR ABCD          2
        TNT1 A             0 A_PlaySound ("Weapons/Pistol/Out", CHAN_Weapon)
        PSTR EFGHIII       2
        TNT1 A             0 A_PlaySound ("Weapons/Pistol/In", CHAN_Weapon)
        PSTR JKLMNOPQRDCBA 2
        PIST A             2
        goto Ready2
    }
}