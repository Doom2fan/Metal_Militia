actor DME_BaseWeapon : weapon {
    weapon.BobRangeX 0.3
	weapon.BobRangeY 0.5
	weapon.BobStyle InverseSmooth
	weapon.BobSpeed 2.0
    inventory.PickupSound "misc/gunpickup"
    weapon.Kickback 100
    +weapon.Ammo_Optional +weapon.NoAutoFire +inventory.Undroppable
    +weapon.NoAlert
    states {
    DryFire:
        TNT1 A 0 A_PlaySound ("Weapons/DryFire", CHAN_Weapon)
        TNT1 A 0 A_Jump (256, "Reload")
        wait
        
    Ready:
        TNT1 A 1 A_WeaponReady
        loop
    Fire:
        TNT1 A 1
        goto Ready

    KickMelee:
        TNT1 A   0 A_TakeInventory ("DME_DoMelee", 99999)
        KICK BCD 1
    KickMelee.Mid:
        TNT1 A 0 A_JumpIfInventory ("PowerStrength", 1, "KickMelee.Berserk")
        TNT1 A 0 A_CustomPunch (random [weaponDamage] (1, 5) * 13, 1, 0, "DME_MeleePuff")
        goto KickMelee.End
    KickMelee.Berserk:
        TNT1 A 0 A_CustomPunch (3 * random [weaponDamage] (1, 5) * 13, 1, 0, "DME_MeleePuffStrong")
        goto KickMelee.End
    KickMelee.End:
        KICK H        4
        KICK IGFEDCBA 1
        TNT1 A        0 A_Jump (256, "Ready2")
        wait
    
    Select:
    BaseSelect:
        TNT1 A 0 A_Jump (256, "TrueSelect")
        wait
    TrueSelect:
        TNT1 A 0 A_Raise
        wait
    Deselect:
    BaseDeselect:
        TNT1 A 0 A_JumpIfHealthLower (1, "Deselect.DEAD")
        TNT1 A 0 A_Jump (256, "TrueDeselect")
        wait
    TrueDeselect:
        TNT1 A 0 A_Lower
        wait
    Deselect.DEAD:
        TNT1 A 1 A_Lower
        wait
    }
}

// By Jekyll Grim Payne/zer0, edited by Chronos "phantombeta" Ouroboros
actor DME_MeleePuff {
    +noBlockmap +noGravity +puffOnActors -allowParticles
    +noExtremeDeath
    damageType Kick
    attackSound "Weapons/Kick/HitWall"
    activeSound "Weapons/Kick/Swing"
}

actor DME_MeleePuffStrong : DME_MeleePuff {
    decal FistScorch
    attackSound "Weapons/Kick/Strong"
    activeSound "Weapons/Kick/Swing"
    -noExtremeDeath +extremeDeath
    states {
    Spawn:
        TNT1 A 0 noDelay A_SpawnItem ("DME_ShotSmoke")
        stop
    }
}

// SynthFire stuff
// Synth-Fire|By Xaser
actor DME_SynthFireActive         : boolean { }
actor DME_SynthFireLeft           : boolean { }
actor DME_SynthFireRight          : boolean { }
actor DME_SynthFireRightReloading : boolean { }
actor DME_SynthFireLeftReloading  : boolean { }